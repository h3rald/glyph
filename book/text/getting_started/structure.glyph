
	p[Every Glyph project contains a code[document.glyph] file that is typically used to define the document structure. The default code[document.glyph] generated automatically when creating a new project is the following:]

	highlight[=html|
book[
  @frontmatter[
    toc[]
    preface[
      @title[Preface]
      todo[Write the preface]
      include[preface]
    ]
  ]
  @bodymatter[  
    chapter[ 
      @title[Chapter 1]
      todo[Write chapter 1]
      include[chapter_1]
    ]
    chapter[
      @title[Chapter 2]
      todo[Write chapter 2]
      include[chapter_2]
    ]
  ]
  @backmatter[
    appendix[
      @title[Appendix A]
      todo[Write appendix A]
      include[appendix_a]
    ]
  ]
]
	=]

	textile[
Even without knowing anything about &[glang], you can easily figure out that this file defines a document with a Table of Contents, a Preface some Chapters and an Appendix.

As you can see, Glyph wraps portions of text within square brackets preceded by an identifier. These identifiers are used for em[\.=>[#macro_intro|macros]] and em[\.=>[#attribute_intro|attributes]]. The only syntactic difference between macros and attributes is that attributes are preceded by a code[@]. 

For now, think about a macro as something that performs a certain action and -- generally -- produces some text output or manipulation of the text inside it. In this way, it becomes easy to understand that the @chapter@ macro creates a chapter and the %>[include] includes an external file, for example.
Attributes "belong" to the macro they're in, so in this case the %>[book] has the following attributes:
*  code[@frontmatter]
*  code[@bodymatter]
*  code[@backmatter]

More specifically, in this @document.glyph@ file:
* The %>[book] wraps every other macro and is used to create the document header and default title page.
* Then, the code[@frontmatter], code[@bodymatter], and code[@backmatter] attributes are used to divide the portions of your document according to the rules of =>[http://en.wikipedia.org/wiki/Book_design|book design]. They are not mandatory, but they can be used, for example, to number your appendixes with letters instead of numbers and similar.
* @preface@, @chapter@, @appendix@ are just a way to wrap content in @<div>@ tags, from an HTML point of view, but they are also necessary to nest the content of your document and generate the Table of Contents automatically, together through code[@title] attributes.
	]
